plugins {
  id 'java'
  id 'application'
  id 'org.javamodularity.moduleplugin' version '1.8.12'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
}

group 'fr.expenses'
version '1.0.0'

repositories {
  mavenCentral()
}

ext {
  junitVersion = '5.10.2'
}

sourceCompatibility = '23'
targetCompatibility = '23'

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

application {
  mainModule = 'fr.expenses.tableau'
  mainClass = 'fr.expenses.tableau.HelloApplication'
}

javafx {
  version = '17.0.6'
  modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

  implementation 'org.xerial:sqlite-jdbc:3.36.0.3'
  implementation 'org.slf4j:slf4j-api:2.0.9'
  implementation 'ch.qos.logback:logback-classic:1.4.11'
}

test {
  useJUnitPlatform()
}

jlink {
  imageZip = project.file("${buildDir}/distributions/ExpensesManager-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'ExpensesManager'
  }
  
  jpackage {
    def currentOs = org.gradle.internal.os.OperatingSystem.current()
    
    imageOptions = []
    installerOptions = []
    installerName = 'ExpensesManager'
    appVersion = project.version
    
    if (currentOs.windows) {
      installerType = 'msi'
      installerOptions += ['--win-dir-chooser', '--win-menu', '--win-shortcut']
      installerOptions += ['--vendor', 'Expenses Manager']
      installerOptions += ['--description', 'Gestionnaire de dépenses développé en JavaFX']
      installerOptions += ['--copyright', 'Copyright (c) 2025']
      installerOptions += ['--icon', "${projectDir}/src/main/resources/fr/expenses/tableau/icon.png"]
    }
    if (currentOs.linux) {
      installerType = 'deb'
      installerOptions += ['--linux-shortcut', '--linux-menu-group', 'Office']
      installerOptions += ['--icon', "${projectDir}/src/main/resources/fr/expenses/tableau/icon.png"]
    }
    if (currentOs.macOsX) {
      installerType = 'dmg'
      installerOptions += ['--mac-sign']
      installerOptions += ['--icon', "${projectDir}/src/main/resources/fr/expenses/tableau/icon.png"]
    }
  }
}

jlinkZip {
  group = 'distribution'
}

task createInstaller(dependsOn: 'jpackage') {
  group = 'distribution'
  description = 'Génère un installateur pour le système courant'
}